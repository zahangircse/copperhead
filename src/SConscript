import os
import inspect
import fnmatch

# try to import an environment first

Import('env')
env = env.Clone()

Import('libcopperhead')

abscur = os.path.abspath(os.curdir)
parent, current = os.path.split(os.path.abspath(os.curdir))
backend_dir = os.path.join(parent, 'backend')
backend_inc_dir = os.path.join(backend_dir, 'inc')
env.Append(CPPPATH = [backend_inc_dir])

import distutils
#Add Python includes
env.Append(CPPPATH = [distutils.sysconfig.get_python_inc()])
env.Append(LIBS = ['boost_python'])

try:
    Import('siteconf')
except:
    siteconf = {}

bid = siteconf.get('BOOST_INC_DIR', None)
bld = siteconf.get('BOOST_LIB_DIR', None)
if bid:
    env.Append(CPPPATH = [bid])
if bld:
    env.Append(LIBPATH = [bld])

if env['PLATFORM'] == 'darwin':
    env.Append(LINKFLAGS=['-F/System/Library/Frameworks/', '-framework', 'Python'])


cppenv = env.Clone()
cudaenv = env.Clone()


cppenv.Append(CCFLAGS = ["-std=c++0x", "-Wall", "-O3"])
libcopperhead_path, tail = os.path.split(str(libcopperhead[0]))
cppenv.Append(LIBPATH = [libcopperhead_path])
cppenv.Append(LIBS = ["copperhead"])

#Find numpy include files
import numpy
numpyinc = os.path.join(os.path.dirname(numpy.__file__),
                        os.path.join('core', 'include'))
cudaenv.Append(CPPPATH = [numpyinc])

cudaenv.Append(LIBS = ['cudart'])
cudaenv.Append(CPPPATH = [os.path.join(backend_dir,
                                         os.path.join('library', 'prelude'))])


cudata_object = cudaenv.SharedObject('copperhead/runtime/cudata_impl.cu')
cudaenv_host = cudaenv.Clone()
cudaenv_host.Append(CCFLAGS = ["-std=c++0x", "-Wall", "-O3"])
cudaenv_host.Append(LIBPATH = [libcopperhead_path])
cudaenv_host.Append(LIBS = ["copperhead"])


extensions = []

#Build backend compiler
python_wrap_object = cppenv.SharedObject('copperhead/compiler/python_wrap.cpp')
backend_compiler = cppenv.SharedLibrary(source=['copperhead/compiler/backendcompiler.cpp',
                                                python_wrap_object], SHLIBPREFIX='',SHLIBSUFFIX='.so')
cppenv.Requires(backend_compiler, libcopperhead)
extensions.append(('copperhead/compiler/backendcompiler.cpp', backend_compiler))

#Build backendsyntax, backendtypes
for x in ['copperhead/compiler/backendsyntax.cpp',
          'copperhead/compiler/backendtypes.cpp']:
    ext = cppenv.SharedLibrary(source=x,
                               SHLIBPREFIX='',
                               SHLIBSUFFIX='.so')
    cppenv.Requires(ext, libcopperhead)
    extensions.append((x, ext))

#Build cudata
for x in ['copperhead/runtime/cudata.cpp']:
    ext = cudaenv_host.SharedLibrary(source=[x, cudata_object],
                                     SHLIBPREFIX='',
                                     SHLIBSUFFIX='.so')
    cudaenv_host.Requires(ext, libcopperhead)
    extensions.append((x, ext))

Return('extensions')
