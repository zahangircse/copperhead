import os
import inspect
import fnmatch

# try to import an environment first

Import('env')
env = env.Clone()


abscur = os.path.abspath(os.curdir)
parent, current = os.path.split(os.path.abspath(os.curdir))
backend_dir = os.path.join(parent, 'backend')
backend_inc_dir = os.path.join(backend_dir, 'inc')
env.Append(CPPPATH = [backend_inc_dir])

import distutils
#Add Python includes
env.Append(CPPPATH = [distutils.sysconfig.get_python_inc()])
env.Append(LIBS = ['boost_python'])

try:
    Import('siteconf')
except:
    siteconf = {}

bid = siteconf.get('BOOST_INC_DIR', None)
bld = siteconf.get('BOOST_LIB_DIR', None)
np_inc_path = siteconf.get('NP_INC_PATH', None)
if bid:
    env.Append(CPPPATH = [bid])
if bld:
    env.Append(LIBPATH = [bld])

if env['PLATFORM'] == 'darwin':
    env.Append(LINKFLAGS=['-F/System/Library/Frameworks/', '-framework', 'Python'])


cppenv = env.Clone()
cudaenv = env.Clone()


cppenv.Append(CCFLAGS = ["-std=c++0x", "-Wall", "-O3"])

#Find numpy include files
import numpy
numpyinc = os.path.join(os.path.dirname(numpy.__file__),
                        os.path.join('core', 'include'))
cudaenv.Append(CPPPATH = [numpyinc])

cudaenv.Append(LIBS = ['cudart'])
cudaenv.Append(CPPPATH = [os.path.join(backend_dir,
                                         os.path.join('library', 'prelude'))])


cudaenv_host = cudaenv.Clone()
cudaenv_host.Append(CCFLAGS = ["-std=c++0x", "-Wall", "-O3"])


extensions = []

#Build backend compiler
python_wrap_object = cppenv.SharedObject('copperhead/compiler/python_wrap.cpp')
backend_compiler = cppenv.SharedLibrary(source=['copperhead/compiler/backendcompiler.cpp',
                                                python_wrap_object], SHLIBPREFIX='',SHLIBSUFFIX='.so')
extensions.append(('copperhead/compiler/backendcompiler.cpp', backend_compiler))

cppenv_aug = cppenv.Clone()
cppenv_aug.Append(CPPPATH=[np_inc_path])

cunp_library = cppenv_aug.SharedLibrary('copperhead/runtime/cunp.cpp')
extensions.append(('copperhead/runtime/cunp.cpp', cunp_library))

#Build backendsyntax, backendtypes
for x in ['copperhead/compiler/backendsyntax.cpp',
          'copperhead/compiler/backendtypes.cpp',
          'copperhead/runtime/load.cpp']:
    ext = cppenv.SharedLibrary(source=x,
                               SHLIBPREFIX='',
                               SHLIBSUFFIX='.so')
    extensions.append((x, ext))


    
#Build cudata
for x in ['copperhead/runtime/cudata.cpp',
          'copperhead/runtime/cuda_info.cpp']:
    ext = cudaenv_host.SharedLibrary(source=[x],
                                     SHLIBPREFIX='',
                                     SHLIBSUFFIX='.so')
    extensions.append((x, ext))

Return('extensions')
